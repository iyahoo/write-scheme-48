## 再帰的なパーサ: リスト・ドット対・クォートの処理

ここからは演習問題の部分も既に読んでいることを前提にしている。

```haskell
parseList :: Parser LispVal
parseList = liftM List $ sepBy parseExpr spaces
```

`sepby` は[ここ](https://hackage.haskell.org/package/parsec-3.1.11/docs/Text-Parsec-Combinator.html)で定義されている。コンマをセパレーターとして `p` というパーサーを繰替えす例は以下のようになるようだ。

```haskell
commaSep p  = p `sepBy` (symbol ",")
```

よって本文中の `sepBy parseExpr spaces` は空白区切りの全ての式をパースする。`List` は既に `LispVal` に入っているので実行して確認しておく。

```haskell
$ stack ghci
...
Ok, modules loaded: Parser, Datatype, Main.
*Main Datatype Parser> :l "src/Parser.hs"
...
Ok, modules loaded: Parser, Datatype.
*Parser> parse parseList "lisp" "1 2 3"
Right (List [Number 1,Number 2,Number 3])
*Parser> parse parseList "lisp" "\"string\" #t"
Right (List [String "string",Bool True])
```

次はドットリストだが、まず Scheme におけるドットリストというものを確認しておくと、別名ドット対などとも呼ばれる Scheme だけでなく Lisp 全体の基本となるデータ構造である。Haskell におけるタプルのような形をしている。

```scheme
gosh> (cons 'a 'b)
(a . b)
gosh> (cons 'a '())
(a)
gosh> (cons 'a (cons 'b (cons 'c '())))
(a b c)
gosh> (cons 'a (cons 'b (cons 'c 'd)))
(a b c . d)
```

上で示したように Lisp におけるリストは終端に空のリストが来るドットリストの連結であるので、これを指して基本となるデータ構造と表した。今のところ Haskell で実装するパーサーでは `'()` に関する処理は実装されていないのでこのような振る舞いは実装されない。ではドットリスト用のパーサーを実装を見る。

```haskell
parseDottedList :: Parser LispVal
parseDottedList = do
    head <- endBy parseExpr spaces
    tail <- char '.' >> spaces >> parseExpr
    return $ DottedList head tail
```

まず `endBy` は `sepBy` と同じページで定義されており、振る舞いは殆ど同じで違いはセパレーターが最後にも来るということのみ。

> >>を使ってパーサアクションの並びを繋ぎあわせて、その全体をdo文の右辺に使うことができるのに注意してください。char '.' >> spacesはParser ()型で、parseExprと組み合わせることでParser LispVal型を得ますが、それは正にこのdoブロック全体の型です。

do文 (do-statement) とは `head <- exp` の部分を指す。型を意識すること。

> 次に、Schemeのシングルクォートを使った構文糖衣のサポートを加えましょう。

構文糖衣とは、ある表記を簡単にするために用意されている省略形のようなもの、Schemeにおいては以下のように二つの式が全く同じ意味を持つ。

```scheme
gosh> (quote 3)
3
gosh> '3
3
```

つまり `(quote x)` を基本として、`'x` も `(quote x)` としてパースする。

```haskell
parseQuoted :: Parser LispVal
parseQuoted = do
   char '\''
   x <- parseExpr
   return $ List [Atom "quote", x]
```

つぎに `paseExpr` を実装する。

<!-- 演習無し状態に切り替え -->
```haskell
parseExpr :: Parser LispVal
parseExpr = parseAtom
        <|> parseString
        <|> parseNumber
        <|> parseQuoted
        <|> do char '('
               x <- try parseList <|> parseDottedList
               char ')'
               return x
```

> これはParsecの最後の機能の説明となっています: バックトラックです。 parseListとparseDottedListはドットまでは同じ文字列を認識し、そのことはそれぞれの選択肢が失敗する前に入力を消費してはならないという要求に反します。tryコンビネータは指定されたパーサを実行しようとしますが、それが失敗すると、入力を元の状態にまで戻します。これにより、パーサの選択時に他の選択肢に影響を与えることなく入力を消費するパーサを使うことができます。

この中の

> parseListとparseDottedListはドットまでは同じ文字列を認識し、そのことはそれぞれの選択肢が失敗する前に入力を消費してはならないという要求

これは、次の文字をひとつ先読みしてどのパーサーを実行するかという判断を行う構文解析上の要求である。例えば `parseQuoted` では

```haskell
parseQuoted :: Parser LispVal
parseQuoted = do
   char '\''
   x <- parseExpr
   return $ List [Atom "quote", x]
```

入力された文字列を受けとった時パーサーは、その文字列の先頭を見てこのパーサーが成功するかを判断するのであり、文字列を消費することはしない。しかし、文字列の先頭が `'` であった場合には `char '\''` というパーサのパースが成功し、全体の文字列からは落とされる。これが `parseList` で `try` を利用している理由になる。というのも、もし `parseList` でリストの要素を順々に処理している途中で `.` というものが表れた場合、それは予期しない入力として `parseList` は失敗し、そこで全体のパース処理は終了してしまうためである。
　この後 wiki ではタイムラインで確認を行っているが、結果がわかりづらいのでこれまでの通り ghci 上で確認する。

```haskell
$ stack ghci
...
Ok, modules loaded: Parser, Datatype, Main.
*Main Datatype Parser> :l "src/Parser.hs"
...
Ok, modules loaded: Parser, Datatype.
*Parser> parse parseExpr "lisp" "(a test)"
Right (List [Atom "a",Atom "test"])
*Parser> parse parseExpr "lisp" "(a (nested) test)"
Right (List [Atom "a",List [Atom "nested"],Atom "test"])
*Parser> parse parseExpr "lisp" "(a (doted . list) test)"
Right (List [Atom "a",DottedList [Atom "doted"] (Atom "list"),Atom "test"])
*Parser> parse parseExpr "lisp" "(a '(quoted (dotted . list)) test)"
Right (List [Atom "a",List [Atom "quote",List [Atom "quoted",DottedList [Atom "dotted"] (Atom "list")]],Atom "test"])
*Parser> parse parseExpr "lisp" "(a '(imbalanced parens)"
Left "lisp" (line 1, column 24):
unexpected end of input
expecting space or ")"
*Parser> 
```
