## 練習問題 2
練習問題となっているがかなり時間がかかる。またこの演習の一部は後に回しても良いかもしれない。ここでのことは後々必須となるので教科書の続きとして扱ってよいと思う (もちろん可能であれば時間が許す限り自分で考えてみた方がよい)。

### 練習問題 2-1

- do 記法
```haskell
parseNumber :: Parser LispVal
parseNumber = do
  x <- many1 digit
  return . Number . read $ x
```

- bind 記法

```haskell
parseNumber :: Parser LispVal
parseNumber = many1 digit >>= \x -> return . Number . read $ x
```

### 練習問題 2-2

問題が少し分かりづらいが、現在のパーサーではエスケープが実装されていないので以下のように動作してしまう。

```
*Parser> parse parseString "lisp" "\"ho\\\"ge\""
Right (String "ho\\")
```

つまり `"` が来た時点で文字列の終了として扱ってしまう。したがって

```
*Parser> parse parseString "lisp" "\"ho\\\"ge\""
Right (String "ho\"ge")
```

このように動作するパーサーを実装したい。まずは `parseEscape` というバックスラッシュと何かの組合せのパーサーを考える。以降の問題でこれを拡張するのでこのような関数名にしておく。

```haskell:Parser.hs
parseEscape :: Parser Char
parseEscape = char '\\' >> char '"'
```

```haskell
parseString :: Parser LispVal
parseString = do char '"'
                 x <- many $ parseEscape <|> noneOf "\""
                 char '"'
                 return $ String x
```

`many $ parseEscape <|> noneOf "\""` という式で、いくつかのエスケープ文字 (現在はスラッシュとダブルクオートの組み合わせ) かダブルクオート以外の文字を全て文字列の中身として定義することになる。

### 練習問題 2-3

```haskell
parseEscape :: Parser Char
parseEscape = do
  char '\\'
  x <- oneOf "\"nrt\\"
  return $ case x of
             'n' -> '\n'
             'r' -> '\r'
             't' -> '\t'
             _   -> x
```

確認してみる

```haskell
putStrLn "\\hoge\tfuga\nfuga\thoge\\"
\hoge	fuga
fuga	hoge\
```
これと全く同じ動作をする文字列を試すと

```haskell
*Parser> parse parseString "lisp" "\"\\\\hoge\\tfuga\\nfuga\\thoge\\\\\""
Right (String "\\hoge\tfuga\nfuga\thoge\\")
```

パースに成功しているが、正しく動作しているのかわかりずらいので確認するために `parse` の結果を受け取って表示する `printString` を実装しておく。`parse` の型は

```haskell
*Parser> :t parse parseString "lisp" "\"ho\\\"\n\r\t\\\\ge\""
parse parseString "lisp" "\"ho\\\"\n\r\t\\\\ge\""
  :: Either ParseError LispVal
```

であるので

```haskell
printString :: Either ParseError LispVal -> IO ()
printString (Right (String s)) = putStrLn s
printString _ = putStrLn "Print error"
```

これで確認してみると

```haskell
*Parser> printString $ parse parseString "lisp" "\"\\\\hoge\\tfuga\\nfuga\\thoge\\\\\""
\hoge	fuga
fuga	hoge\
```

正しくパースできているのがわかる。



### 練習問題 2-4
(TODO: ここでは `try` というものを利用しておりこれについてはこの章の後半で扱う。これを一部の演習は後に回しても良いかと考えた理由だが、もしかしたら `try` を使わずに実装できるかもしれないのでこれを考える。)  
この問題は4時間ほど要した。これはまず scheme の方でどのように振る舞うかを確認する

```scheme
gosh> #b1101
13
gosh> #o777
511
gosh> #xabc
2748
gosh> #d10
10
```

このように `#b`・`#o`・`#x`・`#d`の後に来た数字をそれぞれ2進数・8 進数・16 進数・10 進数として値を読み込みこんで10進数表示するように、`parseNumber` を変更していく。まず Haskell の `Numeric` module には `redOct` と `readHex` はあるが2進数用の関数は無いので同じく `Numeric` module にある `readInt` という関数を用いて実装する必要がある。まず `readHex` のふるまいを確認しておくと (:m で ghci にモジュールをインポートできる)

```haskell
*Parser> :m Numeric
*Parser Numeric> :t readHex
readHex :: (Num a, Eq a) => ReadS a
Parser Numeric> readHex "abhogeab"
[(171,"hogeab")]
```

今回のパーサーでは `#b` などの後に続く数字だけを `many1 digit` で取りだすので、タプルの第2要素の変換できなかった文字列を気にする必要はない。(パーサー部分でタプルの第2要素が空であることを保証する) `readInt` を使用した `readBin` は以下のようになる。 `digitToInt` は module `Data.Char` で定義されている。

```haskell
readBin :: (Num a, Eq a) => ReadS a
readBin = readInt 2 (\c -> elem c "01") digitToInt
```

`readInt` の第一引数は基数を、第二引数は文字がその基数に対してふさわしい値であるかどうかの述語 (今回の2進数では `0` か `1` のどちらかを表わすために `elme c "01"` としている)。 最後はその述語を満たした文字を対応する `Int` に変換する関数を取る。

```haskell
*Parser> readBin "1111"
[(15,"")]
```

ここで私は `parseNumber` を以下のように実装した

```haskell
parseNumber :: Parser LispVal
parseNumber = do
  top <- char '#' <|> digit
  case top of
    '#' -> do
      unit <- oneOf "bodx"
      rest <- many1 digit
      return $ Number . fst . head . (case unit of
                                        'b' -> readBin
                                        'o' -> readOct
                                        'd' -> readDec
                                        'x' -> readHex) $ rest
    _   -> liftM (Number . read . (:) top) $ many digit
```

そして `parseExpr` の順番を以下のように交換することにより実装できると考えたが、

```haskell
parseExpr :: Parser LispVal
parseExpr = parseNumber
            <|> parseAtom
            <|> parseString
```

```haskell
*Parser> parse parseExpr "lisp" "#b110hoge"
Right (Number 6)
*Parser> parse parseExpr "lisp" "#b110"
Right (Number 6)
*Parser> parse parseExpr "lisp" "110"
Right (Number 110)
*Parser> parse parseExpr "lisp" "#o110"
Right (Number 72)
*Parser> parse parseExpr "lisp" "#x110"
Right (Number 272)
```

値自体のパースは上手くいっている((実際には"#xabdf"のような16進数を受けつけないバグがある。後ほど出てくる実装ではそれに対応している)。しかし

```haskell
*Parser> parse parseExpr "lisp" "#t"
Left "lisp" (line 1, column 2):
unexpected "t"
```

このように `parseAtom` で定義されている `#t` がうまく扱われなくなる。ここで `再帰的なパーサ: リスト・ドット対・クォートの処理` で導入された `try` を利用する。これは通常パーサーが戻れるのは一文字のみであり (正確には一字先読みというものであり、入ってくる文字列を消費せずに一文字だけ確認して次に来るものがどのようなものかを判断する)、`parseNumber` の `top <- char '#' <|> digit` でどちらかにマッチした時点でその後の `parseNumber` が失敗しても、私が順番を変更した `parseExpr` における、`parseNumber` の次の `parseAtom` に行くということはしてくれないからだ。これではどう実現すればいいのかわからなくなったが、 http://hackage.haskell.org/package/parsec-3.1.11/docs/Text-Parsec-Prim.html#v:try にある `try` と `string` を用いれば実現できそうだとういうことを見つけることができた ((諦めかけていた。これ以降 https://hackage.haskell.org/package/parsec-3.1.11/docs/Text-Parsec-Prim.html や https://hackage.haskell.org/package/parsec-3.1.11/docs/Text-Parsec-Char.html を参照して使えるパーサーを探す必要が出てくる。参照している場所は使用している `Text.ParserCombinators.Parsec` とは違うがこれらの module を import して、一部変更して使っているようだ。https://hackage.haskell.org/package/parsec-3.1.11/docs/src/Text.ParserCombinators.Parsec.Prim.html#try などを参考))。つまり `try` というのは、`try` に渡したパーサーが失敗してもそれが消費した分の文字列を復元してくれるパーサーを生成する。
　以下の `parseNumber` がそれを使ったものである。

```haskell
parseNumber :: Parser LispVal
parseNumber = do
  s <- many1 digit
       <|> try (string "#b") <|> try (string "#o")
       <|> try (string "#d") <|> try (string "#x")
  if isDigit . head $ s
    then return . Number . read $ s
    else case s of
           "#b" -> many1 (oneOf "01") >>= toNumber . readBin
           "#o" -> many1 (oneOf "01234567") >>= toNumber . readOct
           "#d" -> many1 (oneOf "0123456789") >>= toNumber . readDec
           "#x" -> many1 (oneOf "0123456789abcdef") >>= toNumber . readHex
  where toNumber = return . Number . fst . head
```

```haskell
*Parser> parse parseExpr "lisp" "#t"
Right (Bool True)
*Parser> parse parseExpr "lisp" "#f"
Right (Bool False)
*Parser> parse parseExpr "lisp" "#b1010111"
Right (Number 87)
*Parser> parse parseExpr "lisp" "#o777"
Right (Number 511)
*Parser> parse parseExpr "lisp" "#xabcdef"
Right (Number 11259375)
*Parser> parse parseExpr "lisp" "3093"
Right (Number 3093)
*Parser> parse parseExpr "lisp" "#hoge"
Right (Atom "#hoge")
```

長くなったので、ここまでの段階でのプログラムは https://github.com/iyahoo/write-scheme-48/tree/53458af07357c82d92fd66098b8130dc8008e327 で見ることができる。※注意: これ以降、このようにして途中状態のソースのコミットを貼るようにするが、プログラム以外にも doc なども推敲・まとめ前であり、ほぼメモの段階の半端な状態なのでそちらを参照しないように。

### 練習問題 2-5

まず

```Haskell
parseChar :: Parser LispVal
parseChar = do
  try (string "#\\")
  c <- try (string "newline") <|> try (string "space") <|> letter
```

ここまで書いて問題に気がつく。

1. `string "newline"` や `string "space"` は返り値の型が `Text.Parsec.Prim.ParsecT s u m String` というモナド+ `String` の型をしており `letter` はモナド + `Char` なので型が合わない
2. `#\` の後には1文字だけ来るので `many letter` を置くわけにはいかない

ここで、このように型が合わないという現象が起きた時点で、何か実装を疑うべきなのかもしれない。型が合わないということは、何かを間違えているのではないかと疑ってみる。今回はひとまず `latter` の振舞いをしてかつリストにして返す Parser が無いかを探してみたが見つからなかったので無理矢理実装する。 `do` 記法を使い、マッチした `Char` をリストに入れることで、`string "newline"` などに型を合わせる。そして残りの部分を `case` を用いて実装する 

```haskell
parseChar :: Parser LispVal
parseChar = do
  try (string "#\\")
  c <- try (string "newline") <|> try (string "space")
       <|> do { x <- anyChar; return (x:[])}
  return . Character $ case c of
                         "newline" -> '\n'
                         "space"   -> ' '
                         _         -> head c
```

`do` 記法をワンライナーで書く場合はセミコロン `;` を利用する。 そして https://hackage.haskell.org/package/parsec-3.1.11/docs/Text-Parsec-Char.html にある `anyChar` というものが目的の動作をすると思われるのでこれを利用した。`parser` は基本的にあるマッチした文字 (又は文字列) をモナドと共に返すので、`x <- anyChar; return (x:[])` の部分でマッチしたある文字をリストに入れることで1文字からなる文字列にし、他の `<|>` の項の型に合わせている。`do` 記法はあくまでモナドを用いていること以外は他の `式` と何も変わらないのでこのようにある式の途中に入れることが可能である。

### 練習問題 2-6

`LispVal` に `Float Double` を追加する。

```haskell
data LispVal = Atom String
             | List [LispVal]
             | DottedList [LispVal] LispVal
             | Number Integer
             | Float Double
             | String String
             | Bool Bool
             | Character Char
             deriving (Show)
```

まず簡単に思いつくのは以下のような単純な `parseFloat` というパーサーを定義することである。

```Haskell
parseFloat :: Parser LispVal
parseFloat = do
  b <- many1 digit
  _ <- char '.'
  x <- many1 digit
  return . Float . read $ b ++ "." ++ x
```

文字列である少数部をどのように Lisp の値として認識させるかに少し困ったが、 Haskell の `read` 関数を利用してしまうことで簡単にできた。これを `parseExpr` に加えると (この時 `parseNumber` の後ろに加えてしまうと少数の点の前までで `Number` としてパースされてしまうのでその前に置く。)

```haskell
parseExpr :: Parser LispVal
parseExpr = try parseFloat
            <|> parseNumber
            <|> parseChar
            <|> parseAtom
            <|> parseString
```

```haskell
$ stack ghci
...
*Main Datatype Parser> :l "src/Parser.hs"
...
Ok, modules loaded: Parser, Datatype.
*Parser> parse parseExpr "l" "39393"
Right (Number 39393)
*Parser> parse parseExpr "l" "39393.3939"
Right (Float 39393.3939)
```

このように正しく動作する。しかしここまでで気になるのはこの `try` というものである。これは本来なら一字先読みしかできないパーサーに対して複数の文字を先読みすることを可能にする便利なものとして利用しているが、複数の文字を読んで、失敗した場合は戻るという処理をする以上、一字先読みよりも格段に取り回す情報が増えコストが増大するはずでありできるだけ避けたい？

### 練習問題 2-7

Scheme の値 number には complex (複素数), real (実数), rational (有理数), integer (整数)が存在する。ここまでで実数と整数のパーサーは実装しているので残りを実装する。まず複素数。Scheme では `3+2i` や `5-i`, `5.0+0.1i` という入力をすると複素数として扱われるようだ。 http://www.schemers.org/Documents/Standards/R5RS/HTML/r5rs-Z-H-9.html#%_sec_6.2.5 更に `#b1011+i` なども正しく複素数として認識するので、すでに実装した `parseNumber` や `parseFloat` とほとんど同じ部分であるのでパーサーが文字列を生成するまでの部分を別な関数として取り出すことで、複素数用のパーサーでも使えるようにする。まずは以前実装したパーサーを変更していく。変更前をコメントとして添付しておく。まずは `parseNumber` を、Number としてパースした部分を文字列として返すように書き変える。

```haskell
-- 変更前
-- parseNumber :: Parser LispVal
-- parseNumber = do
--   s <- many1 digit
--        <|> try (string "#b") <|> try (string "#o")
--        <|> try (string "#d") <|> try (string "#x")
--   if isDigit . head $ s
--     then return . Number . read $ s
--     else case s of
--            "#b" -> many1 (oneOf "01")               >>= toNumber . readBin
--            "#o" -> many1 (oneOf "01234567")         >>= toNumber . readOct
--            "#d" -> many1 (oneOf "0123456789")       >>= toNumber . readDec
--            "#x" -> many1 (oneOf "0123456789abcdef") >>= toNumber . readHex
--   where toNumber = return . Number . fst . head

parseNumber' :: Parser String
parseNumber' = do
  s <- many1 digit
       <|> try (string "#b") <|> try (string "#o")
       <|> try (string "#d") <|> try (string "#x")
  if isDigit . head $ s
    then return s
    else case s of
           "#b" -> many1 (oneOf "01")               >>= toNumber . readBin
           "#o" -> many1 (oneOf "01234567")         >>= toNumber . readOct
           "#d" -> many1 (oneOf "0123456789")       >>= toNumber . readDec
           "#x" -> many1 (oneOf "0123456789abcdef") >>= toNumber . readHex
  where toNumber = return . show . fst . head

parseNumber :: Parser LispVal
parseNumber = do
  x <- parseNumber'
  return . Number . read $ x
```

`parseNumber` を `parseNumber'` と `parseNumber` に分割している。`parseNumber'` の型は `Parser String` になっているので注意すること。変更内容は `parseNumber` から `Number` を取り出し、また `readOct` などは `String` を `Int` に変換するので `toNumber` 関数で一旦 `String` に戻している。同様に `parseFloat` も変更する。

```haskell
parseFloat' = do
  b <- many1 digit
  dot <- char '.'
  x <- many1 digit
  return $ b ++ [dot] ++ x

parseFloat :: Parser LispVal
parseFloat =  parseFloat' >>= return . Float . read
```

次に `parseComplex` の実装に入るが、少し注意が必要なのは虚部は `i` だけでも良いということと、 Haskell の `read` 関数は "-39" は読み込むことができるが "+39" はできないので分岐が必要になる。他は練習問題 2-6 と似たように実装する。まず複素数を表す型だが

> 例えば、有理数は分母と分子の組で、複素数は実数部と虚数部の組で表すことができます。

という通り、タプルを使うことにする。実部虚部には実数も入ることができるので、タプルの各要素は `Double` とすることにして、整数で入力されたものも `Double` に変換することにした。この仕様は Scheme の実装の Gosh と同様であり、`read` 関数が暗にやってくれる。(コンストラクタ `Complex` は `(Double,Double)` という型の値を受けとるので、`read` した値を `Comply` に渡すようにしてあげれば Haskel が `read` の型を決めてくれる)

```haskell
data LispVal = Atom String
             | List [LispVal]
             | DottedList [LispVal] LispVal
             | Number Integer
             | Float Double
             | Complex (Double,Double)
             | String String
             | Bool Bool
             | Character Char
             deriving (Show)
```

```haskell
parseComplex :: Parser LispVal
parseComplex = do
  r <- try parseFloat' <|> try parseNumber'
  s <- char '+' <|> char '-'
  i <- try (many parseFloat') <|> try (many parseNumber')
  _ <- char 'i'
  return . Complex $ (read r, readImaginary s i)
  where readImaginary '+' [] = 1.0
        readImaginary '-' [] = (-1.0)
        readImaginary '+' i  = read . head $ i
        readImaginary '-' i  = (*) (-1) . read . head $ i
```

複素数は、1文字以上の数値 (ここが省略されることはない) が来た後に `+` か `-` が来て、その後に 0 文字以上の数字が来て (`many` を付けることで `[]` の場合は虚数部の値が省略されていることを表現している) 最後に必ず `i` が来ることになる。また `where` 部で宣言している `readImaginary` 関数で `s` と `i` による値を決めている。これを `parseExpr` に追加する。

```haskell
parseExpr :: Parser LispVal
parseExpr = try parseComplex
            <|> try parseFloat
            <|> parseNumber
            <|> parseChar
            <|> parseAtom
            <|> parseString
```

またこの時、複素数の実数部をパースした時点で値として認識してしまうので `parseFloat` や `parseNumber` より先に置く必要があることに注意すること。実際に試してみると

```haskell
$ stack ghci
...
*Main Datatype Parser> :l "src/Parser.hs"
...
Ok, modules loaded: Parser, Datatype.
*Parser> parse parseExpr "lisp" "333+3i"
Right (Complex (333.0,3.0))
*Parser> parse parseExpr "lisp" "333.00+3.00i"
Right (Complex (333.0,3.0))
*Parser> parse parseExpr "lisp" "333.00+i"
Right (Complex (333.0,1.0))
*Parser> parse parseExpr "lisp" "#b110111+i"
Right (Complex (55.0,1.0))
```

(ここで既知のバグとして `many` を使っている関係で

```haskell
*Parser> parse parseExpr "lisp" "#b110111+301#b11101i"
Right (Complex (55.0,301.0))
```

という風に虚数部で値をいくつか並べてもパースが成功してしまう。`head` を取っているので虚数部の先頭が値として認識されている。これを解決するには、`try` の様に何かパーサーを受けとって、成功すれば list に入ったそれを、成功しなかった場合は `[]` を返すようなものが必要だろうか。今回これの実装は行っていない。)

次に有理数を実装する。scheme では以下のようにふるまう。

```scheme
gosh> 2/3
2/3
gosh> 4/6
2/3
gosh> 3/3
1
```

という様に、分数表示をそのまま分数として扱うためのものである。通分を行い、整数に直せる場合は直す必要がある。また

```scheme
gosh> #b1101/3
*** READ-ERROR: Read error at "(standard input)":line 15: bad numeric format: "#b1101/3"
gosh> 3/
*** ERROR: unbound variable: |3/|
gosh> /3
*** ERROR: unbound variable: /3
```

のように値の省略できず、10進数表記以外はうけつける必要はない。まずは `LispVal` に `Rational` を追加する。複素数の時と似たように、整数型のタプルを用いる

```haskell
data LispVal = Atom String
             | List [LispVal]
             | DottedList [LispVal] LispVal
             | Number Integer
             | Float Double
             | Complex (Double,Double)
             | Rational (Integer,Integer)
             | String String
             | Bool Bool
             | Character Char
             deriving (Show)
```

通分は、分子と分母の最大公約数で両方を割って上げればよい。整数になるのは分子を分母で割ったときの余りが 0 の場合であるので、パーサーの実装は以下のようになる。

```haskell
parseRational :: Parser LispVal
parseRational = do
  n <- many1 digit
  _ <- char '/'
  d <- many1 digit
  let nume = read n; deno = read d -- numerator (分子), denominator (分母)
  let gcdn = gcd nume deno
  let nume' = div nume gcdn; deno' = div deno gcdn -- 通分
  let remn = rem nume' deno'
  toLispVal nume' deno' remn
  where toLispVal n d 0 = return . Number $ (div n d)
        toLispVal n d _ = return . Rational $ (n, d)
```

値の使い回しが多いので `let` を使っている。((TODO: もっときれいに書けないか)) `where` でパターンマッチを利用して分岐の処理をしている。 Haskell ではこのようにパターンマッチを利用して分岐を書くことができ、また `if` よりもプログラムを読んだ時に直感的にわかりやすいということから、 `if` が使われることは少ないようだ。最後に `parseExpr` に追加して実行するのだが、 `parseExpr` の項数が多くなってきたのでこれらの値に関するパーサーはすべて `parseNum` としてまとめることにする。

```haskell
parseNum :: Parser LispVal
parseNum = try parseRational <|> try parseComplex <|> try parseFloat <|> parseNumber

parseExpr :: Parser LispVal
parseExpr = parseNum
            <|> parseChar
            <|> parseAtom
            <|> parseString
```

試してみよう。

```haskell
$ stack ghci
...
*Main Datatype Parser> :l "src/Parser.hs"
...
Ok, modules loaded: Parser, Datatype.
*Parser> parse parseExpr "lisp" "2/3"
Right (Rational (2,3))
*Parser> parse parseExpr "lisp" "6/9"
Right (Rational (2,3))
*Parser> parse parseExpr "lisp" "10/2"
Right (Number 5)
*Parser> parse parseExpr "lisp" "10/1"
Right (Number 10)
*Parser>
```
