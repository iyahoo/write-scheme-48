# 評価器作成の手始め

ここからは、新しいファイル `Eval.hs` に実装していく。

```haskell
-- Eval.hs
module Eval where

import Datatype (LispVal(..))
```

```haskell
-- Parser.hs
module Parser (readExpr) where

...(省略)
import Eval -- 追加
```

```haskell
-- scheme48.cabal exposed-modules に Eval を追加
library
  hs-source-dirs:      src
  exposed-modules:     Parser
                     , Datatype
                     , Eval 
  build-depends:       base >= 4.7 && < 5
                     , parsec
  default-language:    Haskell2010
```

ここから本文

> プログラム片に値を割り当てることによって。

ここまででは、まだ `"3"` という文字をパースして `Number 3` というデータを作っているだけで、これが数学上の 3 というものと同じ意味を持っているかどうかは定義されていない。「プログラム片に値を割り当てる」というのはこういったデータを Haskell 上の値と対応付けることにより実際に計算などが行えるようにしていくことを意味する。
　(注意：前章で ghci 上で試すために `LispVal` を `Show` のインスタンスにしていたが、混乱を避けるためにここで一旦削除する

```haskell
-- Datatype.hs
data LispVal = Atom String
             | List [LispVal]
             | DottedList [LispVal] LispVal
             | Number Integer
             | Float Double
             | Complex (Double,Double)
             | Rational (Integer,Integer)
             | String String
             | Bool Bool
             | Character Char
```
)
　まずは `LispVal` を Scheme の表示と対応付けていく。

```haskell
-- Eval.hs
showVal :: LispVal -> String
showVal (String contents) = "\"" ++ contents ++ "\""
showVal (Atom name) = name
showVal (Number contents) = show contents
showVal (Bool True) = "#t"
showVal (Bool False) = "#f"
```

> パターンは任意の深さに入れ子にすることができ、マッチングは内から外、左から右の順番で行なわれます。関数定義の節は書かれた順にマッチするまで試されます。もしこの説明が難しく感じられても、これからさらに深く評価器について学ぶ時、深く入れ子になったパターンにいくつかあたることになります。

これが意味するのは例えば `LispVal` の `List` である。このコンストラクタは `[LispVal]` を取るので次のように入れ子で表現できるのでこれを指している。

```haskell
showVal (List (Number contents:_)) = ...
```

> ListとDottedList節は似たように働きますが、中のリストを文字列に変換するヘルパー関数unwordsListを定義する必要があります。

ヘルパー関数というのは、ある関数の中である特定の処理をすることに使われる関数で、`showVal` から見た場合はヘルパーとして扱われるが、今回はこの関数に通常通りの宣言を行うので、他の関数との違いはない。(`where` 宣言を使った場合との対比)

```haskell
showVal (List contents) = "(" ++ unwordsList contents ++ ")"
showVal (DottedList head tail) = "(" ++ unwordsList head ++ " . " ++ showVal tail ++ ")"
```

> unwordsList関数はHaskellプレリュードのリスト中の文字列をスペースで接ぎ合わせるunwords関数のように働きます。

`unwords` を確認しておく。

```haskell
*Eval> :t unwords 
unwords :: [String] -> String
*Eval> unwords ["hoge", "fuga"]
"hoge fuga"
```

次にこれを用いた `unwordsList`。

```haskell
unwordsList :: [LispVal] -> String
unwordsList = unwords . map showVal
```

> unwordsListの定義は引数を含みません。これはpoint-free style、つまり関数定義を関数合成と部分適用のみで書く手法の例です。個々の値や"points"にかかずらわる代わりに、いくつかの組み込み関数の組み合わせを使います。まず、mapをshowValに部分適用すると、LispValのリストを取り、それらの文字列表現のリストを返す関数が出来ます。Haskellの関数はカリー化されています。これは、mapのような二引数関数が実際は一引数関数を返す関数だということを意味します。結果として、引数を一つだけ与えた場合、好きなようにできる一引数関数を得ることができます。この場合はそれをunwordsと合成します。すなわち、map showValはLispValのリストをそれらの文字列表現のリストに変え、unwordsがそれをスペースで繋ぎ合わせます。

例を考えてみる。

```haskell
multiply :: Integer -> Integer -> Integer
multiply x y =  x * y
```

これは単に `multiply` に与えた二つの値を掛け合わせる関数である。これをまず `y` を省略して書く。

```haskell
multiply :: Integer -> Integer -> Integer
multiply x =  (x *)
```

これは更に `x` を省略することもでき以下のように書ける。

```
multiply :: Integer -> Integer -> Integer
multiply = (*)
```

ここで重要になるのは、`x` や `y` が省略できるという点よりも、全体として型が変化していないことにある。まず部分適用などの単語について説明する。`(x *)` という書き方が部分適用と呼ばれ、これを可能にしているのがカリー化と呼ばれるものである。カリー化の単純な原理として `multiply` は以下のような無名関数をネストさせたものと同じものと考えることができる。

```haskell
multiply :: Integer -> Integer -> Integer
multiply = \y -> \x -> x * y
```

これによって以下のようなことができる。

```haskell
*Eval> :t multiply 3
multiply 3 :: Integer -> Integer
```

これはつまり `multiply 3` というように部分適用するとその結果として `\x -> x * 3` というような無名関数がひとつだけはずれた新しい関数が返ることになる。これがカリー化の原理である。「全体として型が変化していないこと」という話に戻る。

```haskell
multiply :: Integer -> Integer -> Integer
multiply x y =  x * y
```

これの場合、「Integer 型の値を二つ受け取って Integer 型の値を返す関数」ととれる。

```haskell
multiply :: Integer -> Integer -> Integer
multiply x =  (x *)
```

これの場合は、「Integer 型の値を受けとって、 Integer 型の値を受けとって Integer 型の値を返す関数を、返す」と読める。

```
multiply :: Integer -> Integer -> Integer
multiply = (*)
```

最後にこれは、「Integer 型の値を二つ受けとって Integer 型の値を返す関数を、返す」と読める。このように、上記の三つというのは本質的に全く同じものとして見ることができる。つまり point-free style では構文上で実現されている省略記法ではなく、実際には引数を書いた関数宣言も書かない関数宣言も全く同じものであるということによる。ただし、引数の順番というものが問題になることもある。というのは `multiply = x * y` というのは `multiply = \y -> \x -> x * y` であって、`multiply = \x -> \y -> x * y` ではない。
<!-- ponitrree(点を考えない，矢印を考える).pointwise(点を考える)と言う感じ -->

> 上で使ったshowはどんなShowクラスのインスタンス型の値も文字列に変換する関数です。LispValも同じように扱いたいので、showメソッドとしてshowValを用いるShowクラスのメンバーにします。  
> 型クラスの完全な解説はこのチュートリアルの範囲を越えています。

`上で遣ったshow` というのは `showVal (Number contents) = show contents` で使われている `show` 関数のこと。Haskell においてクラスと呼ばれるものはある関数を持つ集合を指す。クラス事にその関数というのは種類はもちろん数も違っており、今回の `Show` クラスという集合に属するためには `show` 関数というものを実装している必要があり、あるクラスに属している型のことをそのクラスにおけるインスタンスであると言う。今回はデータ型 `LispVal` を `Show` クラスのインスタンスに
するために、`show` という、その型に含まれるコンストラクタ (`LispVal` における `Atom` や `List`) を `show` 関数に渡した時にどのような文字列に写すかを定義した関数を実装する。今回の場合、 `showVal` として実装したものがまさにそれであるので、次のような宣言を行う。

```haskell
instance Show LispVal where
  show = showVal
```

これによって既存の型と同様に `show` 関数を用いて `LispVal` 型の値を文字列に写せるようになる。

 この例だけでは `Show` クラスのインスタンスにすることの利点がわからないが、Haskell の関数の宣言時などには以下のように型に対してコンテキストを持たせることができる。

```haskell
showValue :: Show a => a -> String
showValue a = show a
```

このように書くことで、関数 `showValue` が取る関数を `Show` クラスのインスタンスだけとすることが可能になる。また既存の関数にもこのようにコンテキストを持っている関数が非常に多く、そのような関数に対して自分で宣言した型を利用する場合に必要になる。  
　本題に戻る。`Parser.hs` で定義している `readExpr` を変更する。

```haskell
readExpr input = case parse parseExpr "lisp" input of
    Left err -> "No match: " ++ show err
    Right val -> "Found " ++ show val
```

これで実際にコマンドラインでもパース結果を表示できるようになる。

```haskell
$ stack build
...
Registering scheme48-0.1.0.0...
$ stack exec scheme48-exe "(1 2 2)"
Found (1 2 2)
$ stack exec scheme48-exe "(1 3 (\"this\" \"one\"))"
Found (1 3 ("this" "one"))
```

